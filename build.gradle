import de.itemis.mps.gradle.*

//will pull the groovy classes/types from nexus to the classpath
buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://artifacts.itemis.cloud/repository/maven-mps' }
    }
    dependencies {
        // classpath 'de.itemis.mps:mps-gradle-plugin:1.2.175.cc60dc8'
        classpath('de.itemis.mps:mps-gradle-plugin:2.0.315.0da7e4b')
    }
}

plugins {
    id 'base'
//    id 'maven-publish'
//    id 'co.riiid.gradle' version '0.4.2'
    id 'nu.studer.credentials' version '3.0'
}

apply plugin: 'download-jbr'


// Project version
def major = '2021'
def minor = '1'

//Specify used gradle version
wrapper {
    gradleVersion '7.3.1'
    distributionType Wrapper.DistributionType.ALL
}

configurations {
    mps
    languageLibs
    junitAnt
}

downloadJbr {
    jbrVersion = '11_0_12-b1504.28'
}

// Detect jdk location, required to start ant with tools.jar on classpath otherwise javac and tests will fail
def jdk_home = file("$buildDir/jbrDownload/jbr/Contents/Home")

project.logger.info 'Using JDK at {}', jdk_home
println "Using JDK at: ${jdk_home}"

// Check JDK location
//if (!new File(jdk_home, "lib").exists()) {
//    throw new GradleException("Unable to locate JDK home folder. Detected folder is: $jdk_home")
//}

ext.jdk_home = jdk_home
ext.artifactsDir = new File(buildDir, 'artifacts')
// Dependency versions
ext.mpsVersion = '2021.1.4'


ext.iets3OpenSourceVersion = "$major.$minor.+"
ext.dependencyRepositories = [
        'https://artifacts.itemis.cloud/repository/maven-mps',
        'https://artifacts.itemis.cloud/repository/maven-iets3'
]




task resolveMps(type: Copy) {
    dependsOn configurations.mps
    from {
        configurations.mps.resolve().collect { zipTree(it) }
    }
    into "$buildDir/mps"
}

task resolveAllLanguageLibs(type: Copy) {
    dependsOn("downloadJbr")
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from {
        def configs = [configurations.languageLibs]
        configs.collectMany { it.resolve() }.collect { zipTree(it) }
    }
    into "$buildDir/dependencies"
}


task buildAllScripts(type: BuildLanguages, dependsOn: [resolveMps, resolveAllLanguageLibs]) {
    script.set("$buildDir/scripts/build-allScripts.xml")
}

task buildLanguages(type: BuildLanguages, dependsOn: buildAllScripts) {
    script.set("$buildDir/scripts/build-languages.xml")
}

task packageLanguages(type: Zip, dependsOn: buildLanguages) {
    archiveBaseName = 'org.itemis.phydev'
    from artifactsDir
    include 'org.itemis.phydev/**'
}

assemble.dependsOn packageLanguages
// Default arguments for ant scripts (additionally to what is set in the MPS-build-solution)
def defaultScriptArgs = [
        'mps.home'                          : resolveMps.destinationDir,
        'build.dir'                         : buildDir,
        'version'                           : version,
        'mps.generator.skipUnmodifiedModels': true //enable incremental build
]

def defaultScriptClasspath = project.configurations.junitAnt.fileCollection { true } +
        project.files("$ext.jdk_home/lib/tools.jar")

// enables https://github.com/mbeddr/mps-gradle-plugin#providing-global-defaults
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = defaultScriptArgs.collect { "-D$it.key=$it.value".toString() }
ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = defaultScriptClasspath
ext["itemis.mps.gradle.ant.defaultJavaExecutable"] = new File(jdk_home, 'bin/java')

repositories {
    mavenLocal()
    maven { url 'https://artifacts.itemis.cloud/repository/maven-mps' }
    mavenCentral()
}

dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
    languageLibs "org.iets3:opensource:$iets3OpenSourceVersion"
    junitAnt 'org.apache.ant:ant-junit:1.10.6'
}
